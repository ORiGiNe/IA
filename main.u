requireFile("core/Sonar.u");
requireFile("core/Radar.u");
requireFile("core/FLogger.u");
requireFile("core/Robot.u");

requireFile("config.u");

//var gaop = UGaop.new(tty);

//----------------------//
//          IA          //
//----------------------//

/* Toute l'IA est codé en supposant que le robot par de la zone rouge.
 * La fonction Robot.rotate permet de tourner à droite ou à gauche selon que
 * l'on est réellement dans la zone rouge ou dans la zone violette.
 */


// le robot
var robot = Robot.new(0,0,0,"R")|


/* Les différents tag que l'on va utiliser.
 * On rajoute un slot "done". On s'en sevira pour indiquer si le tag est
 * arriver à la fin de son exécution ou s'il a été stopé en cours de route
 */
var Tag.done = false|;

var group_t = Tag.new("group")|
var alones_t = Tag.new("alones")|
var poussoirs_t = Tag.new("poussoirs")|
var u_t = Tag.new ("U")|
var goBackInv_t = Tag.new ("goBackInv")|
var goBack_t = Tag.new ("goBack")|
var cross_t = Tag.new("cross")|

// cette variable sera égal au tag en cours
var current_t|


// launch (action, tag);
function launch {
    var tag = call.evalArgAt(1);
    current_t = tag;
    current_t: call.evalArgAt(0);
    echo("I'm waiiiiting ! Waiting for " + tag + " " + tag.done);
    waituntil(tag.done);
}|;



// les différentes actions du robot



/* goBack
 *
 * Fait demie tour dans le sens des aiguilles d'une montre et ramène son magot
 */
function goBack {
    do (robot) {
         if (_y > 126) {
            // on se ramène dans le bonne axe
            setAngle(0);
            forwardAtX(236);
        };

        setAngle(-90);
        forwardAtY(50);
        rotate(-90);
        forwardAtX(75);
        rotate(-45);
        forwardAtX(30);
        forwardAtX(64);
    };

    goBack_t.done = true;
}|;

at (goBack_t.enter?) goBack_t.done = false;

at (goBack_t.leave?) {
    if (!goBack_t.done) {
        current_t = Tag.new();
        for (4) {
            sleep(0.5s);
            if (!goBack_t.done) {
                current_t:
                    goBack;
            }
            else break;
        };

        launch(u, u_t);
        goBack_t.done = true;
    }
};       



/* U
 *
 * On tente de faire tour de l'île (un U) pour rafler tous les CDs qui trainent
 * dans la mer (on est un peu écolo sur les bords). On part par la droite de l'île.
 */
function u {
    do (robot) {
        rotate(45);
        forwardAtY(50);
        rotate(-45);
        forwardAtX(234);
        rotate(90);
        forwardAtY(126);
        rotate(45);
        forwardAtY(170);
        rotate(45);
        forwardAtX(64);
        rotate(90);
        forwardAtY(100);
        rotate(-45);
        forwardAtX(30);
        forwardAtX(64);
    };

    u_t.done = true;
}|;

at (u_t.enter?) u_t.done = false;

at (u_t.leave?) {
    if (!u_t.done) {
        current_t = Tag.new();
        
        for (4) {
            sleep(0.5s);
            if (!u_t.done) {
                current_t:
                    u;
            }
            else break;
        };

        launch(goBack, goBack_t);
        u_t.done = true;
    };
};


/* Poussoirs
 *
 * Va pousser le poussoir le plus proche.
 *
 * Condition initiale : le robot doit être dans l'axe d'une des deux lignes
 * noires.
 */
function poussoirs {
    do (robot) {
        // on check très grossièrement où on est
        if (_x < 150) {
            // on est près du premier poussoirs
            setAngle(0);
            forwardAtX(64);
            rotate(90);
            forwardAtY(198)
        }
        else {
            // on est près du second poussoir
            if (_y < 126) {
                setAngle(0);
                forwardAtX(235);
                rotate(90);
                forwardAtY(170);
            };

            setAngle(180);
            forwardAtX(190);
            rotate(-90);
            forwardAtY(198);
        };
    };
    poussoirs_t.done = true;
}|;

at (poussoirs_t.enter?) poussoirs_t.done = false;

at (poussoirs_t.leave?) {
    if (!poussoirs_t.done) {
        current_t = Tag.new();
        for (6) {
            sleep(0.5s);
            if (!poussoirs_t.done) {
                current_t:
                    poussoirs;
            }
            else break;
        };

        poussoirs_t.done = true;
    };
};


function cross {
	do (robot) {
		var x;

		if (_x < 150)
			// on est à droite, on veut aller à gauche
			x = 235
		else
			// chemin inverse
			x = 75;

		if (_y < 100)
			// on passe par en haut
			forwardAtY(50)
		else
			// on passe par en bas
			forwardAtY(170);

		setAngle(0);
		forwardAtX(x)
	};
	cross_t.done = true;
}|;

at (cross_t.enter?) cross_t.done = false;

at (cross_t.leave?) {
	if (!cross_t.done)
		cross_t.done = true;
};

// les 4 CDs du bas plus le premier poussoir
/*function group {
    current_t = group_t |
    group_t:
        do (robot) {
            forward(25);
            rotate(90);
            forward(50);
            rotate(90);
            forward(25);
        };
}|;*/


{sleep(3); {robot.stop (current_t)}},
launch(u, u_t);
launch(poussoirs, poussoirs_t);
echo("x = " + robot._x)|
echo("y = " + robot._y)|
launch(cross, cross_t);
echo("x = " + robot._x)|
echo("y = " + robot._y)|
{sleep(6); {robot.stop (current_t)}},
launch(poussoirs, poussoirs_t);
echo("x = " + robot._x)|
echo("y = " + robot._y)|

System.shutdown;
